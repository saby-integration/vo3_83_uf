
&НаКлиенте
Процедура ОткрытьВСБИС_ПолучитьUID( Команда, ПараметрКоманды ) Экспорт 
	МодульОбъекта = ПолучитьМодульОбъекта();
	
	мСсылок = ПолучитьСписокСсылокНаОбъектыКоманды( ПараметрКоманды );
	ПроверкаОбмена = МодульОбъекта.ПроисходилЛиОбменДокументовСоСБИС(мСсылок.Источник);
	Если ПроверкаОбмена.ОбменаНебыло.Количество()  > 0 Тогда
		ДопПарметры		= Новый Структура("Данные, Форма", ПроверкаОбмена, ПараметрКоманды.Форма );
		ДействиеВопроса = Новый ОписаниеОповещения("ОтветНаВопросОтправитьДокументыВСБИС", ЭтаФорма, ДопПарметры);
		ПоказатьВопрос(ДействиеВопроса, 
		"Среди выбранных для открытия документов есть "+
		Формат(ПроверкаОбмена.ОбменаНебыло.Количество(), "ЧГ=0")+
		" которые в СБИС не загружались. Загрузить их?",
		РежимДиалогаВопрос.ДаНетОтмена,0,КодВозвратаДиалога.Да,"Документ не найден в СБИС");	
	Иначе
		ОткрытьВСБИС_ПолучитьURL( ПроверкаОбмена.БылОбмен, ПараметрКоманды );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросОтправитьДокументыВСБИС(Результат, Параметры) Экспорт // здесь, думаю, комм
	Если Результат = КодВозвратаДиалога.Да Тогда
		мСсылок = ПолучитьСписокСсылокНаОбъектыКоманды(Параметры.Данные.ОбменаНебыло);
		ОткрытьФорму("ВнешняяОбработка.СБИС.Форма.ЗагрузкаПечатныхФормДокументов", мСсылок, Параметры.Форма);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		//Получим ссылки на обработанные документы и откроем их в окнах
		ОткрытьВСБИС_ПолучитьURL( Параметры.Данные.БылОбмен, Параметры );
	Иначе
		//При отмене как и полагается не будем ни чего делать
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСБИС_ПолучитьURL_ПослеАутентификации(Результат, Параметры) Экспорт 
	Если Результат <> Неопределено Тогда
		ОткрытьВСБИС_ПолучитьURL( Параметры.Источник, Параметры.ПараметрКоманды );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСБИС_ПолучитьURL( Источник, ПараметрКоманды ) Экспорт 
	МодульОбъекта = ПолучитьМодульОбъекта();
	
	ОшибкаВыполнения	= Ложь;
	Попытка
		СсылкиНаДокументыВСБИС = МодульОбъекта.ПолучитьСсылкиНаДокументыВСБИС( Источник );
	Исключение
		ОшибкаВыполнения	= Истина;
		ИнфоОбОшибке = ИнформацияОбОшибке();
		СтруктураОшибки = МодульОбъекта.ExtExceptionAnalyse(ИнфоОбОшибке);
		Если СтруктураОшибки.type = "Unauthorized" Тогда
			//Открыть форму аутентификации
			ВходяшиеПараметры	= Новый Структура("Источник, ПараметрКоманды",Источник,ПараметрКоманды);
			ПроверкаВведенныхДанныхАутентификации = Новый ОписаниеОповещения("ОткрытьВСБИС_ПолучитьURL_ПослеАутентификации", ЭтаФорма, ВходяшиеПараметры);
			ОткрытьФорму("ВнешняяОбработка.СБИС.Форма.Вход",,,,,, ПроверкаВведенныхДанныхАутентификации);
			Возврат;
		Иначе
			СтруктураОшибки = МодульОбъекта.ExtExceptionAnalyse(ИнфоОбОшибке);
			//Если эти параметры отсутствуею, то и в настройках параметров подключения находится или Неопределено
			//или пустая структура, значит необходимо вызвать окно авторизации
			Если	Найти(СтруктураОшибки.message, "api_url") > 0 ИЛИ
					Найти(СтруктураОшибки.message, "password") > 0 ИЛИ
					Найти(СтруктураОшибки.message, "login") > 0 ИЛИ
					Найти(СтруктураОшибки.message, "api") > 0 Тогда
				ВходяшиеПараметры	= Новый Структура("Источник, ПараметрКоманды",Источник,ПараметрКоманды);
				ПроверкаВведенныхДанныхАутентификации = Новый ОписаниеОповещения("ОткрытьВСБИС_ПолучитьURL_ПослеАутентификации", ЭтаФорма, ВходяшиеПараметры);
				ОткрытьФорму("ВнешняяОбработка.СБИС.Форма.Вход",,,,,, ПроверкаВведенныхДанныхАутентификации);
				Возврат;
			Иначе
				ПоказатьОповещениеПользователя("Ошибка",,СтруктураОшибки.message,,СтатусОповещенияПользователя.Важное, Новый УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецПопытки;
	
	Если	Не ОшибкаВыполнения И СсылкиНаДокументыВСБИС.ОбработаноСОшибкой.Количество()  > 0 Тогда
		ДопПарметры		= Новый Структура("Данные, Форма", СсылкиНаДокументыВСБИС.ОбработаноСОшибкой, ПараметрКоманды.Форма );
		ДействиеВопроса = Новый ОписаниеОповещения("ОтветНаВопросОтправитьДокументыВСБИС", ЭтаФорма, ДопПарметры);
		ПоказатьОповещениеПользователя(
			"Удалены документы в СБИС",
			, 
			"Среди выбранных для открытия документов есть "+
			Формат(СсылкиНаДокументыВСБИС.ОбработаноСОшибкой.Количество(), "ЧГ=0")+
			" которые были удалены в СБИС. Информация о статусах этих документах была удалена.",
			БиблиотекаКартинок.Ошибка32,
			СтатусОповещенияПользователя.Важное, Новый УникальныйИдентификатор);
	ИначеЕсли Не ОшибкаВыполнения Тогда
		ОткрытьВСБИС( СсылкиНаДокументыВСБИС.ОбработаноУспешно, ПараметрКоманды.Форма );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСБИС( МассивДокументовКОтправке, Форма ) Экспорт 
	Для Каждого СсылкаНаДокумент Из МассивДокументовКОтправке Цикл
		ПараметрыФормы = Новый Структура( "Заголовок, АдресСтраницы");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, СсылкаНаДокумент);
		Уникальность = Новый УникальныйИдентификатор;
		Если МассивДокументовКОтправке.Количество() > 0 Тогда
			Уникальность = СокрЛП(МассивДокументовКОтправке[0]);
		КонецЕсли;
		ОткрытьФорму("ВнешняяОбработка.СБИС.Форма.Browser", ПараметрыФормы, Форма, Уникальность );
	КонецЦикла;
КонецПроцедуры

